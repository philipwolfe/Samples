



    <link rel="stylesheet" href="../../../../../util/style.css">

    <form name="Form" method="post" action="srcview.htm?path=/QuickStart/winforms/samples/VB/printing/Example5/PrintingExample5.src&file=TextFilePrintDocument.vb" id="Form">
<input type="hidden" name="__VIEWSTATE" value="YTB6MTQ3MzAyNjM4MF9hMHpfaHo1ejN4X2Ewel9oejV6MHhfYTB6aHppbm5lcmh0bWxfVGVceHRGaWxlUHJpbnREb2N1bWVudC52YnhfX3h4X3h4X3hfX3g=0a3c59d0" />


        <div class="SampleHeader" style="width:100%">
            <div class="SampleTitle">
                <span id="Title">TextFilePrintDocument.vb</span>
            </div>
            <table id="SourceTable" style="font: 8pt Verdana">
    <tr>
        <td style="padding-right:10;"><b>Print Document Source: </b></td>
        <td><a href='../../../../../winforms/samples/vb/printing/example5/textfileprintdocumentvbsource.htm'>TextFilePrintDocument.vb</a> &nbsp;&nbsp</td>
    </tr>
    <tr>
        <td style="padding-right:10;"><b>Win Form Source: </b></td>
        <td><a href='../../../../../winforms/samples/vb/printing/example5/printformvbsource.htm'>PrintForm.vb</a> &nbsp;&nbsp</td>
    </tr>
</table>

        </div>

        <xmp>'------------------------------------------------------------------------------
'/ <copyright from='1997' to='2001' company='Microsoft Corporation'>           
'/    Copyright (c) Microsoft Corporation. All Rights Reserved.                
'/       
'/    This source code is intended only as a supplement to Microsoft
'/    Development Tools and/or on-line documentation.  See these other
'/    materials for detailed information regarding Microsoft code samples.
'/
'/ </copyright>                                                                
'------------------------------------------------------------------------------
Imports System
Imports System.ComponentModel
Imports System.WinForms
Imports System.Drawing
Imports System.Drawing.Printing
Imports System.IO

Namespace Microsoft.Samples.WinForms.VB.PrintingExample5 

    ' TextFilePrintDocument prints a stream to a printer 
    '
    ' Note: In order to avoid problems closing a file 
    ' if an exception occurs this class simply takes a 
    ' steam and leaves it to the caller to open the file 
    ' to print
    Public Class TextFilePrintDocument 
        Inherits PrintDocument 

        Private printFont As Font
        Private streamToPrint As StreamReader

        Public Sub New(streamToPrint As StreamReader)
            MyBase.New
            Me.streamToPrint = streamToPrint 
        End Sub

        'Override OnBeginPrint to set up the font we are going to use
        Overrides Protected Sub OnBeginPrint(ev As PrintEventArgs) 
            MyBase.OnBeginPrint(ev) 
            printFont = new Font("Arial", 10)
        End Sub

        'Override the OnPrintPage to provide the printing logic for the document
        Overrides Protected Sub OnPrintPage(ev As PrintPageEventArgs) 
            
            MyBase.OnPrintPage(ev) 
            
            Dim lpp As Single = 0 
            Dim yPos As Single =  0 
            Dim count As Integer = 0 
            Dim leftMargin As Single = ev.MarginBounds.Left
            Dim topMargin As Single = ev.MarginBounds.Top
            Dim line as String
            
            'Work out the number of lines per page 
            'Use the MarginBounds on the event to do this
            lpp = ev.MarginBounds.Height  / printFont.GetHeight(ev.Graphics) 

            'Now iterate over the file printing out each line
            'NOTE WELL: This assumes that a single line is not wider than the page width
            'Check count first so that we don't read line that we won't print
            line=streamToPrint.ReadLine()
            while (count < lpp AND line <> Nothing)
                
                yPos = topMargin + (count * printFont.GetHeight(ev.Graphics))
                
                'NOTE WELL: In the PDC Release you must pass a StringFormat to DrawString or the 
                'Print Preview control will not work. 
                ev.Graphics.DrawString (line, printFont, Brushes.Black, leftMargin, yPos, new StringFormat())
                
                count = count + 1
                
                if (count < lpp) then
                    line=streamToPrint.ReadLine()
                end if
            
            End While

            'If we have more lines then print another page
            If (line <> Nothing) Then
                ev.HasMorePages = True 
            Else 
                ev.HasMorePages = False 
            End If 
                
        End Sub

    End Class

End Namespace






</xmp>
        <span id="ErrorMessage" style="color:red"></span>

    </form>
